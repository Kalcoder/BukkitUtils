buildscript {
    ext.kotlin_version = '1.3.60'
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'java-library'
    id "maven-publish"
    id "org.jetbrains.dokka" version "0.10.0"
}

apply plugin: 'java'
apply plugin: 'kotlin'

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'com.github.Kalcoder'
archivesBaseName = "BukkitUtils"
version = '1.1'

dokka {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/dokka"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    archiveClassifier = 'javadoc'
}

task dokkaJar(type: Jar, dependsOn: dokka) {
    from dokka.outputDirectory
    archiveClassifier = "dokka"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/Kalcoder/BukkitUtils")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GPRUSERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("GPRTOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from components.java
            artifact dokkaJar
//            artifact javadocJar
            artifact sourcesJar
        }
    }
}

repositories {
    mavenCentral()
    jcenter()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/groups/public/'
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile 'org.bukkit:bukkit:1.14.1-R0.1-SNAPSHOT'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}

import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }
}
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
