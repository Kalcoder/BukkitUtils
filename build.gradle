plugins {
    id 'java-library'
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'me.Kalcoder'
archivesBaseName = "BukkitUtils"
version = '1.0'

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'BukkitUtils'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'A set of utilities that help with creating Minecraft Bukkit plugins.'
                url 'http://github.com/Kalcoder/BukkitUtils'

                scm {
                    connection 'scm:git:git://github.com/Kalcoder/BukkitUtils.git'
                    developerConnection 'scm:git:ssh://github.com/Kalcoder/BukkitUtils.git'
                    url 'http://github.com/Kalcoder/BukkitUtils'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'kalcoder'
                        name 'Kalcoder'
                        email 'shifted.gaming@outlook.com'
                    }
                }
            }
        }
    }
}

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/groups/public/'
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile 'org.bukkit:bukkit:1.14.1-R0.1-SNAPSHOT'
}

import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }
}
